// Top-level build file where you can add configuration options common to all sub-projects/modules.
import dependencies.Dependencies

buildscript {
    ext.kotlin_version = '1.3.41'

    repositories {
        google()
        jcenter()
        mavenCentral()
        
    }
    dependencies {
        classpath BuildPlugins.GRADLE
        classpath BuildPlugins.KOTLIN
        classpath BuildPlugins.NAVIGATION
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}


subprojects {

    afterEvaluate {
        if (!project.hasProperty("android")) return

        android {
            compileSdkVersion AndroidConfig.COMPILE_SDK

            defaultConfig {
                minSdkVersion AndroidConfig.MIN_SDK
                targetSdkVersion AndroidConfig.TARGET_SDK
                versionCode AndroidConfig.VERSION_CODE
                versionName AndroidConfig.VERSION_NAME
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility AndroidConfig.JAVA_VERSION
                targetCompatibility AndroidConfig.JAVA_VERSION
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            sourceSets {
                main.java.srcDirs += Source.MAIN

                test.java.srcDirs += [Source.TEST, Source.SHARED_TEST]
                test.resources.srcDirs  += [Source.SHARED_TEST_RESOURCES]

                androidTest {
                    java.srcDirs += [Source.ANDROID_TEST, Source.SHARED_TEST]
                    assets.srcDirs  += [Source.SHARED_TEST_RESOURCES]
                }
            }

            testOptions {
                unitTests {
                    includeAndroidResources = true
                }
            }

            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }

        dependencies {
            implementation Dependencies.KOTLIN
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
